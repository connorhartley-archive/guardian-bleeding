plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'ninja.miserable.blossom' version '1.0.1'
}

apply plugin: 'org.spongepowered.plugin'
apply plugin: 'com.github.hierynomus.license'

defaultTasks 'licenseFormat', 'build'

def root = project.getRootProject()

group = root.pluginGroup
version = "${root.spongeVersion}-${root.pluginVersion}-${root.pluginPatch}"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sponge.plugin.id = pluginId

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    compile "org.spongepowered:spongeapi:${root.spongeVersion}-SNAPSHOT"

    // External Dependencies

    compile 'com.github.me4502:Precogs:04c53049ea'
}

blossom {
    def location = 'src/main/java/com/ichorpowered/guardian/api/PluginInfo.java'

    replaceToken '@id@', project.pluginId, location
    replaceToken '@name@', project.pluginName, location
    replaceToken '@version@', version, location
    replaceToken '@description@', project.pluginDescription, location
    replaceToken '@databaseversion@', project.databaseVersion, location
    replaceToken '@elderversion@', project.elderVersion, location
    replaceToken '@precogsversion@', project.precogsVersion, location
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

license {
    header = file('../LICENSE.txt')
    include '**/*.java'

    ignoreFailures = false
    strictCheck = true

    mapping {
        java = 'SLASHSTAR_STYLE'
    }
}

task sourceJar(type: Jar) {
    classifier = 'source'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

jar {
    classifier = 'api'
}

artifacts {
    archives sourceJar
    archives javadocJar
    archives jar
}

build.dependsOn(sourceJar)
build.dependsOn(javadocJar)
build.dependsOn(jar)